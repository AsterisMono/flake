{ inputs, lib, ... }: {
  flake-file.inputs.git-hooks = {
    url = "github:cachix/git-hooks.nix";
    inputs.nixpkgs.follows = "nixpkgs";
  };

  perSystem =
    {
      config,
      pkgs,
      system,
      ...
    }:
    {
      checks = {
        pre-commit-check = inputs.git-hooks.lib.${system}.run {
          src = ./.;
          hooks = {
            nixfmt.enable = true;
            statix = {
              enable = true;
              settings.config = lib.toString ../statix.toml;
            };
          };
        };
      };

      devShells.default = pkgs.mkShell {
        packages = with pkgs; [
          nixd
          nixfmt
          just
          nh
          sops
          age
          ssh-to-age
          nixos-rebuild-ng
          nixos-anywhere
          uv
          sshpass
          # `just prompt-luks-password` asks the user for the LUKS passphrase in
          # a graphical dialog.
          pinentry-gnome3
        ];
        inherit (config.checks.pre-commit-check) shellHook;
        buildInputs = config.checks.pre-commit-check.enabledPackages;
        EDITOR = "zeditor --wait";
      };
    };
}
